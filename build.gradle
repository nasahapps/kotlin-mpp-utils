buildscript {
    ext.kotlin_version = "1.3.11"
    ext.coroutines_version = "1.1.0"

    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
}

apply plugin: "kotlin-multiplatform"
apply plugin: "maven-publish"

group = "com.nasahapps.mpp.utils"
version = "1.0"

kotlin {
    targets {
        fromPreset(presets.jvm, "jvm")

        final def nativeTarget = System.getenv("SDK_NAME")?.startsWith("macos") ? presets.macosX64
                : System.getenv("SDK_NAME")?.startsWith("iphoneos") ? presets.iosArm64
                : presets.iosX64
        fromPreset(nativeTarget, 'native') {
            compilations.main.outputKinds("FRAMEWORK")
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
            }
        }
        jvmMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
            }
        }
        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
                implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
            }
        }
        nativeMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"
            }
        }
    }
}

//android {
//    compileSdkVersion 28
//    defaultConfig {
//        minSdkVersion 21
//    }
//    buildTypes.all {
//        sourceSets.main.setRoot("src/androidMain")
//    }
//}