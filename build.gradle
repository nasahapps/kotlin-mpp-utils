buildscript {
    ext.kotlin_version = "1.3.20"
    ext.coroutines_version = "1.1.1"

    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
}

apply plugin: "kotlin-multiplatform"
apply plugin: "maven-publish"

group = "com.nasahapps.mpp.utils"
version = "1.0.2"

kotlin {
    jvm()
    macosX64()
    iosArm64()
    iosX64()

    sourceSets {
        commonMain.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
        }
        commonTest.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
            implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
        }
        jvmMain.dependencies {
            api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
        }
        jvmTest.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
            implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        }
        nativeMain.dependencies {
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"
        }
        nativeTest
        configure([macosX64Main, iosArm64Main, iosX64Main]) {
            dependsOn nativeMain
        }
        configure([macosX64Test, iosArm64Test, iosX64Test]) {
            dependsOn nativeTest
        }
    }
}
